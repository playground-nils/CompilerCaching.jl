using Test, CompilerCaching

function precompile_test_harness(@nospecialize(f), testset::String)
    @testset "$testset" begin
        precompile_test_harness(f, true)
    end
end
function precompile_test_harness(@nospecialize(f), separate::Bool)
    # XXX: clean-up may fail on Windows, because opened files are not deletable.
    #      fix this by running the harness in a separate process, such that the
    #      compilation cache files are not opened?
    load_path = mktempdir(cleanup=true)
    load_cache_path = separate ? mktempdir(cleanup=true) : load_path
    try
        pushfirst!(LOAD_PATH, load_path)
        pushfirst!(DEPOT_PATH, load_cache_path)
        f(load_path)
    finally
        popfirst!(DEPOT_PATH)
        popfirst!(LOAD_PATH)
    end
    nothing
end

precompile_test_harness("Inference caching") do load_path
    write(joinpath(load_path, "ExampleCompiler.jl"), :(module ExampleCompiler
        using CompilerCaching

        const CC = Core.Compiler

        const InfCacheT = @static if isdefined(CC, :InferenceCache)
            CC.InferenceCache
        else
            Vector{CC.InferenceResult}
        end

        # Results struct for this compiler
        mutable struct CompileResults
            ir::Any
            code::Any
            executable::Any
            CompileResults() = new(nothing, nothing, nothing)
        end

        struct ExampleInterpreter <: CC.AbstractInterpreter
            world::UInt
            cache::CacheView
            inf_cache::InfCacheT
        end
        ExampleInterpreter(cache::CacheView) =
            ExampleInterpreter(cache.world, cache, InfCacheT())

        CC.InferenceParams(::ExampleInterpreter) = CC.InferenceParams()
        CC.OptimizationParams(::ExampleInterpreter) = CC.OptimizationParams()
        CC.get_inference_cache(interp::ExampleInterpreter) = interp.inf_cache
        @static if isdefined(Core.Compiler, :get_inference_world)
            Core.Compiler.get_inference_world(interp::ExampleInterpreter) = interp.world
        else
            Core.Compiler.get_world_counter(interp::ExampleInterpreter) = interp.world
        end
        CC.lock_mi_inference(::ExampleInterpreter, ::Core.MethodInstance) = nothing
        CC.unlock_mi_inference(::ExampleInterpreter, ::Core.MethodInstance) = nothing
        @setup_caching ExampleInterpreter.cache

        emit_code_count = Ref(0)

        function emit_ir(cache, mi)
            interp = ExampleInterpreter(cache)
            typeinf!(cache, interp, mi)
        end

        function emit_code(cache, mi, ir)
            emit_code_count[] += 1
            :code_result
        end

        emit_executable(cache, mi, code) = code

        function precompile(f, tt)
            world = Base.get_world_counter()
            mi = method_instance(f, tt; world)
            cache = CacheView{CompileResults}(:ExampleCompiler, world)

            # For inference-based compilation, check if CI already exists
            ci = get(cache, mi, nothing)
            if ci !== nothing
                res = results(cache, ci)
                if res.executable !== nothing
                    return res.executable
                end
            end

            # Cache miss or incomplete - run inference (creates CI)
            ir = emit_ir(cache, mi)

            # Get the CI created by typeinf!
            ci = get(cache, mi, nothing)
            @assert ci !== nothing "typeinf! should have created CI"
            res = results(cache, ci)
            res.ir = ir

            # Generate code
            res.code = emit_code(cache, mi, res.ir)
            res.executable = emit_executable(cache, mi, res.code)

            @assert res.executable === :code_result
            return res.executable
        end

        end # module
    ) |> string)
    Base.compilecache(Base.PkgId("ExampleCompiler"), stderr, stdout)

    write(joinpath(load_path, "ExampleUser.jl"), :(module ExampleUser
        import ExampleCompiler
        using PrecompileTools

        function square(x)
            return x*x
        end

        ExampleCompiler.precompile(square, (Float64,))

        # identity is foreign
        @setup_workload begin
            @compile_workload begin
                ExampleCompiler.precompile(identity, (Int64,))
            end
        end
        end# module
    ) |> string)

    Base.compilecache(Base.PkgId("ExampleUser"), stderr, stdout)
    @eval let
        using CompilerCaching
        import ExampleCompiler
        @test ExampleCompiler.emit_code_count[] == 0

        cache = CacheView{ExampleCompiler.CompileResults}(:ExampleCompiler, Base.get_world_counter())

        # Check that no cached entry is present
        identity_mi = method_instance(identity, (Int,))
        @test !haskey(cache, identity_mi)

        using ExampleUser
        @test ExampleCompiler.emit_code_count[] == 0

        # importing the package bumps the world age, so get a new cache view
        cache = CacheView{ExampleCompiler.CompileResults}(:ExampleCompiler, Base.get_world_counter())

        # Check that kernel survived
        square_mi = method_instance(ExampleUser.square, (Float64,))
        @test haskey(cache, square_mi)
        ExampleCompiler.precompile(ExampleUser.square, (Float64,))
        @test ExampleCompiler.emit_code_count[] == 0

        # check that identity survived
        @show ext_cis_lost = v"1.12.0-DEV.1268"<=VERSION<v"1.12.5" || v"1.13-"<=VERSION
        @test haskey(cache, identity_mi) broken=ext_cis_lost
        ExampleCompiler.precompile(identity, (Int,))
        @test ExampleCompiler.emit_code_count[] == 0 broken=ext_cis_lost
    end
end
